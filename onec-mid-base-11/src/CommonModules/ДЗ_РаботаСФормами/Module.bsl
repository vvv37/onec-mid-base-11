Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)

	Если Форма.Элементы.Найти("КонтактноеЛицо") <> Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	НовыйЭлемент = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.Д_КонтактноеЛицо"; 
	НовыйЭлемент.Заголовок = "Контактное лицо";

КонецПроцедуры

Процедура ДобавитьПолеКонтактноеЛицоПередСуммаДокумента(Форма)

	Если Форма.Элементы.Найти("КонтактноеЛицо") <> Неопределено Тогда 
		Возврат;
	КонецЕсли;     
	
	НовыйЭлемент = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), ,Форма.Элементы.СуммаДокумента);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.Д_КонтактноеЛицо";     
	НовыйЭлемент.Заголовок = "Контактное лицо";


КонецПроцедуры

Процедура ДополнитьФорму(Форма) Экспорт 
	
	ИмяФормы = Форма.ИмяФормы;                 
		
	Если  ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда 
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);  
		ДобавитьПолеИКомандуСкидка(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда 
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли  ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда 
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли  ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда 
		ДобавитьПолеКонтактноеЛицоПередСуммаДокумента(Форма);
	ИначеЕсли  ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда 
		ДобавитьПолеКонтактноеЛицоПередСуммаДокумента(Форма);   
	КонецЕсли;
	
КонецПроцедуры      

Процедура ДобавитьПолеИКомандуСкидка(Форма)  
	
	НовыйЭлемент = Форма.Элементы.Добавить("ГруппаПолеИКоманда", Тип("ГруппаФормы"),Форма.Элементы.ГруппаШапкаЛево);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;     
	НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
	НовыйЭлемент.ОтображатьЗаголовок = Ложь; 
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;  
	
	НовыйЭлемент = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаПолеИКоманда);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;     
	НовыйЭлемент.УстановитьДействие("ПриИзменении","СогласваннаяСкидкаПриИзменении");
	НовыйЭлемент.ПутьКДанным = "Объект.Д_СогласваннаяСкидка"; 
	НовыйЭлемент.Заголовок = "Согласованная скидка";

	
	НоваяКоманда = Форма.Команды.Добавить("ПересчитатьТоварыИУслуги");
	НоваяКоманда.Заголовок = "Пересчитать таблицу";
	НоваяКоманда.Действие = "ПересчитатьТоварыИУслуги";
	
	НовыйЭлемент = Форма.Элементы.Добавить("ПересчитатьТоварыИУслуги", Тип("КнопкаФормы"), Форма.Элементы.ГруппаПолеИКоманда);
	НовыйЭлемент.Картинка = БиблиотекаКартинок.Обновить;
	НовыйЭлемент.Отображение = ОтображениеКнопки.КартинкаИТекст;;
	НовыйЭлемент.ИмяКоманды = "ПересчитатьТоварыИУслуги";
	
КонецПроцедуры        

